# see http://linuxgazette.net/126/peterson.html
#     http://gnuplot.sourceforge.net/demo_4.2/histograms.html
#
set terminal png size 1200,800
set xdata time
set timefmt "%Y-%m-%d-%H:%M:%S"
set datafile missing '-'
set datafile separator '|'
# time range must be in same format as data file
#set xrange ["2010-09-01-00:00:00":"2011-04-01-00:00:00"]
set auto x
set yrange [0:20]
set grid
set xlabel "Date\\nTime"
set format x "%d.%m.%y"
# weekly ticks
set xtics nomirror rotate by -90 604800
set ylabel "Duration"
set title "Leihs Request Durations in Production"
# get column titles from first data row
set key autotitle columnhead
set key outside box
set style data points
set pointsize 0.25

# combined
set output "reports/production.log.durations.png"
plot "production.log.durations.csv" using 1:2, \
     "" using 1:3, \
     "" using 1:4

# totals only
set output "reports/production.log.totals.png"
plot "" using 1:2

# view only
set output "reports/production.log.view.png"
plot "" using 1:3

# db only
set output "reports/production.log.db.png"
plot "" using 1:4
